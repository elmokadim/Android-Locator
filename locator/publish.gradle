apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

version = '1.0'
group = 'dev.mokadim.locator'

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                groupId 'dev.mokadim.locator'
                artifactId 'android-locator'

                // Add your description here
                name 'Android Locator'
                description 'Locator is an Android library that simplifies the process of requesting current location and location updates at runtime.'
                url 'https://github.com/elmokadim/Android-Locator'

                // Set your license
                licenses {
                    license {
                        name 'GNU General Public License version 3'
                        url 'https://opensource.org/licenses/GPL-3.0'
                    }
                }
                developers {
                    developer {
                        id 'elmokadim'
                        name = 'Ahmed Elmokadim'
                        email = 'elmokadim@gmail.com'
                    }
                }
                scm {
                    connection 'https://github.com/elmokadim/Android-Locator.git'
                    developerConnection 'https://github.com/elmokadim/Android-Locator.git'
                    url 'https://github.com/elmokadim/Android-Locator'

                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

// Bintray
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = bintrayName
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        publish = true
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
            gpg {
                sign = true
                passphrase = properties.getProperty("bintray.gpg.password")
            }
        }
    }
}
